plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group = 'net.minecraftforge'
version = gradleutils.getTagOffsetVersion()
println "Version: $version"

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net' }
}

license {
    header project.file('LICENSE-header.txt')
    include 'net/minecraftforge/mergetool/**/*.java'
    newLine false
}

sourceSets {
    cpwFML
    forgeFML
    forgeAPI
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.mergetool.ConsoleMerger')
        attributes('Implementation-Version': project.version)
    }
    from sourceSets.cpwFML.output
    from sourceSets.forgeFML.output
    from sourceSets.forgeAPI.output
}
tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    archiveClassifier = 'fatjar'
}
tasks.named('sourcesJar', Jar).configure {
    from sourceSets.cpwFML.allSource
    from sourceSets.forgeFML.allSource
    from sourceSets.forgeAPI.allSource
}
tasks.register('cpwFML', Jar) {
    archiveClassifier = 'cpw'
    from sourceSets.cpwFML.output
}
tasks.register('forgeFML', Jar) {
    archiveClassifier = 'forge'
    from sourceSets.forgeFML.output
}
tasks.register('forgeAPI', Jar) {
    manifest.attributes('Automatic-Module-Name': 'net.minecraftforge.mergetool.api')
    archiveClassifier = 'api'
    from sourceSets.forgeAPI.output
}

dependencies {
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'org.ow2.asm:asm-util:9.5'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'net.minecraftforge:srgutils:0.4.15'

    implementation sourceSets.cpwFML.output
    implementation sourceSets.forgeFML.output
    implementation sourceSets.forgeAPI.output
}

artifacts {
    archives shadowJar
    archives cpwFML
    archives forgeFML
    archives forgeAPI
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifact cpwFML
        artifact forgeFML
        artifact forgeAPI

        pom {
            name = 'MergeTool'
            description = 'Merges two jar files together, useful for rebuilding Retroguard stripped jars.'
            url = 'https://github.com/MinecraftForge/MergeTool'
            scm {
                url = 'https://github.com/MinecraftForge/MergeTool'
                connection = 'scm:git:git://github.com/MinecraftForge/MergeTool.git'
                developerConnection = 'scm:git:git@github.com:MinecraftForge/MergeTool.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/MinecraftForge/MergeTool/issues'
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                }
            }

            developers {
                developer {
                    id = 'LexManos'
                    name = 'Lex Manos'
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

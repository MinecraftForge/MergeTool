import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4]'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: $version"

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

tasks.register('copyMarkers', Copy).configure  {
    from(configurations.markers.collect { zipTree(it) } ) {
        into 'markers'
        include '**/*.class'
        rename { it.replace('.class', '.marker') }
    }
    includeEmptyDirs = false
    into project.layout.buildDirectory.dir('copyMarkers')
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.mergetool.ConsoleMerger')
        attributes('Implementation-Version': project.version)
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'fatjar'
}

sourceSets {
    main {
        resources {
            srcDirs += [tasks.named('copyMarkers')]
        }
    }
}

configurations {
    markers
}

dependencies {
    implementation(libs.bundles.asm)
    implementation(libs.jopt.simple)
    implementation(libs.srgutils)

    markers 'net.minecraftforge:mergetool-cpw:1.0'
    markers 'net.minecraftforge:mergetool-fml:1.0'
    markers 'net.minecraftforge:mergetool-api:1.0'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = 'mergetool'

        pom {
            name = 'MergeTool'
            description = 'Merges two jar files together, useful for rebuilding Retroguard stripped jars.'
            url = 'https://github.com/MinecraftForge/MergeTool'

            PomUtils.setGitHubDetails(pom, 'MergeTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

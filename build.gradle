plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'net.minecraftforge'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/mergetool/**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.mergetool.ConsoleMerger')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.mergetool.ConsoleMerger')
    manifest.attributes('Implementation-Version': project.version)
}

artifacts {
    archives jar
    archives shadowJar
}

dependencies {
    compile 'org.ow2.asm:asm:6.2'
    compile 'org.ow2.asm:asm-tree:6.2'
    compile 'org.ow2.asm:asm-util:6.2'
    compile 'com.google.guava:guava:25.1-jre'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
}
configurations { deployJars }
uploadArchives {
    repositories {
        add project.repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty('mavenPassword')) {
            logger.info('Publishing to files server')
            repository(url: 'http://files.minecraftforge.net/maven/manage/upload') {
                authentication(userName: 'forge', password: project.getProperty('mavenPassword'))
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Merges two jar files together, useful for rebuilding Retroguard stripped jars.'
            url 'https://github.com/MinecraftForge/MergeTool'

            scm {
                url 'https://github.com/MinecraftForge/MergeTool'
                connection 'scm:git:git://github.com/MinecraftForge/MergeTool.git'
                developerConnection 'scm:git:git@github.com:MinecraftForge/MergeTool.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/MinecraftForge/MergeTool/issues'
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
            }
        }
    }
}
